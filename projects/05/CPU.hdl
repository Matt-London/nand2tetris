// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Decide if it is an a or c instruction - feed output into a register
    Mux16(a=instruction, b=aluOut, sel=instruction[15], out=aOrCMux);
    And(a=instruction[15], b=instruction[5], out=cLoad);
    Not(in=instruction[15], out=aIns);
    Or(a=cLoad, b=aIns, out=loadAreg);
    ARegister(in=aOrCMux, load=loadAreg, out=aRegOut, out[0..14]=addressM);

    // Setup D register
    And(a=instruction[15], b=instruction[4], out=loadDreg);
    DRegister(in=aluOut, load=loadDreg, out=aluNum1);

    // Feed registers into ALU
    Mux16(a=aRegOut, b=inM, sel=instruction[12], out=aluNum2);

    ALU(x=aluNum1, y=aluNum2, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=aluOut, out=outM, zr=aluZr, ng=aluNg);

    // Assign writeM
    And(a=instruction[3], b=instruction[15], out=writeM);

    // Figure out if it should jump ins[0..2]
    And(a=aluNg, b=instruction[2], out=jumpng);
    And(a=aluZr, b=instruction[1], out=jumpzr);
    Not(in=aluNg, out=aluNotng);
    Not(in=aluZr, out=aluNotzr);
    And(a=aluNotng, b=aluNotzr, out=aluGT);
    And(a=aluGT, b=instruction[0], out=jumpgt);
    Or(a=jumpng, b=jumpzr, out=jumptmp);
    Or(a=jumptmp, b=jumpgt, out=jumpRes);
    And(a=jumpRes, b=instruction[15], out=jump);

    // Reset functionality
    Not(in=jump, out=incPC);
    PC(in=aRegOut, load=jump, inc=incPC, reset=reset, out[0..14]=pc);

}